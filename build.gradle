plugins {
    id 'com.github.ben-manes.versions' version '0.27.0'
}

apply plugin: 'java-library'
apply plugin: 'maven-publish'

allprojects {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    project.group = 'no.fint'

    repositories {
        mavenCentral()
        jcenter()
        maven {
            url 'https://repo.fintlabs.no/releases'
        }
        maven {
            url 'https://dl.bintray.com/fint/maven'
        }
        mavenLocal()
    }
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'groovy'
    apply plugin: 'maven-publish'
    apply from: 'https://raw.githubusercontent.com/FINTLabs/fint-buildscripts/v1.8.0/dependencies.gradle'

    dependencies {
        compileOnly("org.projectlombok:lombok:${lombokVersion}")
    }

    test {
        testLogging {
            events 'passed', 'skipped', 'failed'
        }
    }

    publishing {
        if (project.hasProperty('reposiliteUsername') && project.hasProperty('reposiliteToken')) {
            repositories {
                maven {
                    url "https://repo.fintlabs.no/releases"
                    credentials {
                        username = reposiliteUsername
                        password = reposiliteToken
                    }
                    authentication {
                        basic(BasicAuthentication)
                    }
                }
            }
        }

        publications {
            mavenJava(MavenPublication) {
                from components.java
                afterEvaluate {
                    artifactId = jar.archiveBaseName
                }
            }
        }
    }
}
