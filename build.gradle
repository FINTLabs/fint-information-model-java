plugins {
    id 'com.github.ben-manes.versions' version '0.53.0'
    id "com.diffplug.spotless" version "8.1.0"
}

apply plugin: 'java-library'
apply plugin: 'maven-publish'

allprojects {
    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
        withSourcesJar()
    }

    tasks.withType(JavaCompile).configureEach {
        options.release = 8
        options.compilerArgs += ['-Xlint:-options']
    }

    project.group = 'no.novari'

    repositories {
        maven { url = 'https://repo.fintlabs.no/releases' }
        mavenLocal()
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'groovy'

    configurations {
        annotationProcessor
    }

    dependencies {
        annotationProcessor("org.projectlombok:lombok:${lombokVersion}")
        compileOnly("org.projectlombok:lombok:${lombokVersion}")
        implementation("com.fasterxml.jackson.core:jackson-annotations:2.20")
        implementation("com.fasterxml.jackson.core:jackson-core:2.20.1")
        implementation("javax.validation:validation-api:2.0.1.Final")
        testRuntimeOnly("org.junit.platform:junit-platform-launcher:6.0.2")
    }

    test {
        testLogging {
            events 'passed', 'skipped', 'failed'
        }
    }

    apply plugin: "com.diffplug.spotless"
    spotless {
        java {
            googleJavaFormat("1.33.0")

            removeUnusedImports()
            trimTrailingWhitespace()
            endWithNewline()
        }
    }

    if (!project.name.endsWith('-tests')) {
        apply plugin: 'maven-publish'

        publishing {
            repositories {
                maven {
                    url = "https://repo.fintlabs.no/releases"
                    credentials {
                        username = System.getenv("REPOSILITE_USERNAME")
                        password = System.getenv("REPOSILITE_PASSWORD")
                    }
                }
            }
            publications {
                create('maven', MavenPublication) {
                    from components.java
                }
            }
        }
    }
}
